/** 
 * SARL-MASSIM - Interface between the SARL agent-oriented language 
 * and the MASSIM RMIT 2018+ server
 * Copyright (C) 2017 The SARL-MASSIM Authors.
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
package au.edu.rmit.agtgrp.agtcity.sarl.mw.util

import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_ActionStatusInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_ChargingStationInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_DumpInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_JobInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_ResourceInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_ShopInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_StorageInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_TellEntityInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_TellPlayerTeamInfo
import au.edu.rmit.agtgrp.agtcity.sarl.mw.events.E_WorkshopInfo
import eis.iilang.Percept
import java.util.ArrayList
import java.util.Collection
import java.util.HashMap
import java.util.HashSet
import java.util.List
import java.util.Set
import java.util.function.Predicate
import massim.protocol.scenario.city.data.AuctionJobData
import massim.protocol.scenario.city.data.ChargingStationData
import massim.protocol.scenario.city.data.DumpData
import massim.protocol.scenario.city.data.EntityData
import massim.protocol.scenario.city.data.FacilityData
import massim.protocol.scenario.city.data.ItemData
import massim.protocol.scenario.city.data.JobData
import massim.protocol.scenario.city.data.MissionData
import massim.protocol.scenario.city.data.ResourceNodeData
import massim.protocol.scenario.city.data.RoleData
import massim.protocol.scenario.city.data.ShopData
import massim.protocol.scenario.city.data.StorageData
import massim.protocol.scenario.city.data.WellData
import massim.protocol.scenario.city.data.WorkshopData

/** 
 * Class used to represent a state of the simulation as seen by entities
 * The SARL agent may build or update a state from the percepts received for the many entities it controls
 */
class State implements Cloneable  {

	/*
	 * These data structures keep track of all the information known so far from the game, as received
	 * from all agent players in the team
	 * 
	 * The are all hash maps mapping the name id to an object storing the corresponding information
	 */
	public var id : String
	public var mapName : String
	public var seedCapital : int
	public var simulationLength : int
	public var team : String
	public var money : int
	public var stepNo : int

	/**
	 * Different complex information indexed by their name
 */
	public var entities : HashMap<String, EntityData>
	public var jobs : HashMap<String, JobData>
	public var facilities : HashMap<String, FacilityData>
	public var items : HashMap<String, ItemData>
	public var roles : HashMap<String, RoleData>

	/**
	 * The last step that a given piece of game information was observed.
 */
	public var stepLastSeen : HashMap<String, Integer>

	public var lastStep : boolean = false

	
	/**
	 * Constructor that initializes all of the HashMaps to empty
	 */
	new() {
		this.money = 0

		this.entities = new HashMap()	// other entities seen (only some attributes are observed)
		this.jobs = new HashMap()
		this.facilities = new HashMap()
		this.items = new HashMap()
		this.roles = new HashMap()

		this.stepLastSeen = new HashMap()
	}

	// TODO: This is not useful as is because member classes are not clonable
	def clone() : Object  throws CloneNotSupportedException {
		return super.clone();
	}

	/**
	 * Adds sim-start data to aggregator maintained data-structures.
	 * https://github.com/ssardina-agts/agtcity-server/blob/master/docs/eismassim.md#sim-start-percepts
	 * 
	 * @param percepts - a collection of small percepts that encodes a sim-start message
	 */
	def recordSimStartData(percepts : Collection<Percept>) {
		for(p : percepts){
			switch(p.name){
				case "id" :{
					id = Util.extractString(p.parameters.get(0))
				}
				case "map" : {
					mapName = Util.extractString(p.parameters.get(0))
				}
				case "seedCapital" : {
					seedCapital = Util.extractInt(p.parameters.get(0))
				}
				case "steps" : {
					simulationLength = Util.extractInt(p.parameters.get(0))
				}
				case "team" : {
					team = Util.extractString(p.parameters.get(0))
				}
				case "item": {
					val item = CityDataCreator.createItem(p)
					items.put(item.getName(), item)
				}
				case "role": {
					val role = CityDataCreator.createRole(p)
					roles.put(role.getName(), role)
				}
			}
		}
	}

	/** Update the data of an entity
	 * 
	 * @param entityName entity to update its data object
	 * @param entityData the object containing the state of the entity
	 */
	def updateEntityData(entityName : String, entityData : EntityData) : void {
		entities.put(entityName, entityData)
	}

	/**
	 * Updates the aggregated game state.
	 * 
	 * 1. Filter out facilities, jobs, entities, items from the game percept.
	 * 2. Create/update the Java representation of them within this skill.
	 * 
	 * @param percepts received for the current step.
	 */
	def updateAggregatedGameState(percepts : Collection<Percept>) {
		val stepNo = Util.getStepNo(percepts)
		
		if (stepNo >= this.simulationLength - 1) this.lastStep = true
		
		this.stepNo = stepNo
		for (p : percepts) {
			if (CityDataCreator.FACILITY_TYPES.contains(p.name)) { // p = chargingStation(..) dump(...) shop(...)
				val facility = CityDataCreator.createFacility(p)
				this.facilities.put(facility.name, facility)
				this.stepLastSeen.put(facility.name, stepNo)
			} else if (CityDataCreator.JOB_TYPES.contains(p.name)) { //	p = job(...) or auction(..) or mission(..)
				val job = CityDataCreator.createJob(p)
				this.jobs.put(job.getId(), job)
				this.stepLastSeen.put(job.getId(), stepNo)
			} else if (CityAgentEntityManager.ENTITY_TYPES.contains(p.name)) {	// p = entity(....)
				val entity = CityAgentEntityManager.createEntity(p)
				this.entities.put(entity.getName(), entity)
				this.stepLastSeen.put(entity.getName(), stepNo)
			} else if (p.name.equals("massium")) {
				this.money = Util.extractInt(p.parameters.get(0))
			}
		}
	}

	
	/**
	 * Structured Report of the current state.
	 *
	 * @return report structured as a table
	 */
	def report() : String {
		/* Columns in the printed formatted table. */
		val columns = 1
		/* Width of each column. */
		val columnWidth = "50"

		var tableItems : List<String> = newArrayList()

		for(role : roles.values()) {
			tableItems.add(role.toString())
		}
		for(entity : entities.values()) {
			tableItems.add(entity.toString())
		}
		for(facility : facilities.values()) {
			tableItems.add(facility.toString())
		}
		for(item : items.values()) {
			tableItems.add(item.toString())
		}
		for( job : jobs.values()) {
			tableItems.add(job.toString())
		}

		var formatString = ""
		for (var n : int = 0; n < tableItems.size(); n++) {
			val tableEntry = "%" + (n + 1) + "$-" + columnWidth + "s"
			if (n % columns == 0) {
				formatString += "\n"
			}
			formatString += tableEntry
		}

		/* Return the complete report formatted as a table. */
		return String.format(formatString, tableItems.toArray())
	}


	///////////////////////////////////////////////////////////////////////////////
	//	CHECKERS
	// /////////////////////////////////////////////////////////////////////////////
	
	/**
	 * Have the info from the simulation loaded already in the state?
	 */
	def isSimStartLoaded() : boolean {
		return mapName !== null
	}

	/**
	 * Has the final step of the game been received?
	 */
	def isLastStep() : boolean {
		return lastStep
	}

	/**
	 * Is the entity currently in a facility?
	 * 
	 * @param entityName the name of the entity
	 * @return true if entity is currently in a facility
	 */
	def isAtFacility(entityName : String) : boolean {
		val x = entities.get(entityName)
		
		return (x.facility !== null)
	}

	/** 
	 * Is the entity currently in a shop facility?
	 * 
	 * @param entityName the name of the entity
	 * @return true if entity is currently in a facility
	 */
	def isAtShop(entityName : String) : boolean {
		val entityData = entities.get(entityName)
		return (isAtFacility(entityName) && shops.containsKey(entityData.facility))
	}

	/** 
	 * Is the entity currently in a charging station facility?
	 * 
	 * @param entityName the name of the entity
	 * @return true if entity is currently in a charging station facility
	 */
	def isAtChargingStation(entityName : String) : boolean {
		val entityData = entities.get(entityName)
		return (entityData.facility !== null && chargingStations.containsKey(entityData.facility))
	}

	///////////////////////////////////////////////////////////////////////////////
	//	GETTERS
	// /////////////////////////////////////////////////////////////////////////////

	/** 
	 * Get team member entities with basic details.
	 * 
	 * @return a map from entity names in the team to their current data as EntityData objects
	 */
	def getTeamEntities() : HashMap<String, EntityData> {
		return getMatchingEntities([ entity : EntityData |
			// return true if our team
			return team.equals(entity.team)
		])
	}

	/** 
	 * Get opposition entities with basic details.
	 * 
	 * @return a map from entity names in the opposition team to their current data as EntityData objects
	 */
	def getOppositionEntities() : HashMap<String, EntityData> {
		return getMatchingEntities([ entity : EntityData |
			// return true if not our team
			return !team.equals(entity.team)
		])
	}

	/** 
	 * Get entities matching a predicate with basic details.
	 * 
	 * @param filter a predicate to filter by
	 * @return a map from entity names to entity information as EntityData that match the filter
	 */
	def getMatchingEntities(filter : Predicate<EntityData>) : HashMap<String, EntityData> {
		val filtered_entities : HashMap<String, EntityData> = newHashMap()
		for (entity : entities.values()) {
			/* Return only entities matching the provided predicate. */
			if (filter.test(entity)) {
				filtered_entities.put(entity.name, entity);
			}
		}
		return filtered_entities
	}

	/** 
	 * Get the set of shop names
	 * 
	 * @returns the set of names of known shops
	 */
	def getShopsNames() : Set<String> {
		return shops.keySet()
	}

	/** 
	 * Get the set of facility names
	 * 
	 * @returns the set of names of known facilities
	 */
	def getFacilitiesNames() : Set<String> {
		return facilities.keySet()
	}

	/**
	 * Get all charging stations
	 *
	 * @returns a map from known charging station names to their information as a ChargingStationData object
	 */
	def getChargingStations() : HashMap<String, ChargingStationData> {
		return getFacilities(typeof(ChargingStationData))
	}

	/**
	 * Get all dumps
	 * 
	 * @returns a map from known dumps names to their information as a DumpData object
	 */
	def getDumps() : HashMap<String, DumpData> {
		return getFacilities(typeof(DumpData))
	}

	/**
	 * Get all resource nodes
	 * 
	 * @returns a map from known resource node names to their information as a ResourceNodeData object
	 */
	def getResourceNodes() : HashMap<String, ResourceNodeData> {
		return getFacilities(typeof(ResourceNodeData))
	}

	/**
	 * Get all known shops
	 * 
	 * @returns a map from known shop names to their information as a ShopData object
	 */
	def getShops() : HashMap<String, ShopData> {
		return getFacilities(typeof(ShopData))
	}

	/**
	 * Get all known storages
	 * 
	 * @returns a map from known storage names to their information as a StorageData object
	 */
	def getStorages() : HashMap<String, StorageData> {
		return getFacilities(typeof(StorageData))
	}

	/**
	 * Get all known wells
	 * 
	 * @returns a map from known well names to their information as a WellData object
	 */
	def getWells() : HashMap<String, WellData> {
		return getFacilities(typeof(WellData))
	}

	/**
	 * Get all workshops
	 * 
	 * @returns a map from known workshop names to their information as a WorkshopData object
	 */
	def getWorkshops() : HashMap<String, WorkshopData> {
		return getFacilities(typeof(WorkshopData))
	}

	/**
	 * Get all facilities of a given type
	 *
	 * @param type the class type of the facilities to return
	 * @returns a HsahMap of facilities matching type T
	 */
	private def <T extends FacilityData> getFacilities(type : Class<T>) : HashMap<String, T> {
		val filtered_facilities : HashMap<String, T> = new HashMap();
		// extract only facilities of desired type
		for(facility : facilities.values()) {
			if (type.isInstance(facility)) {
				filtered_facilities.put(facility.name, facility as T);
			}
		}
		return filtered_facilities
	}

	/**
	 * Get all current  regular jobs
	 * 
	 * @returns a map from known regular jobs names to their information as a JobData object
	 */
	def getRegularJobs() : HashMap<String, JobData> {
		val filtered_jobs : HashMap<String, JobData> = new HashMap();
		// extract only regular jobs - these are directly of parent type JobData, filter out all other class types
		for (job : jobs.values()) {
			if (! (job instanceof AuctionJobData || job instanceof MissionData)) {
				filtered_jobs.put(job.getId(), job)
			}
		}
		return filtered_jobs
	}

	/**
	 * Get all auctions
	 * 
	 * @returns a map from known auction names to their information as a AuctionData object
	 */
	def getAuctionJobs() : HashMap<String, AuctionJobData> {
		return getJobs(typeof(AuctionJobData))
	}

	/**
	 * Get all missions
	 * 
	 * @returns a map from known mission names to their information as a MissionData object
	 */
	def getMissionJobs() : HashMap<String, MissionData> {
		return getJobs(typeof(MissionData))
	}

	/**
	 * Get all jobs of a given type
	 *
	 * @param type the class type of the jobs to return
	 * @returns a HashMap of jobs matching type T
	 */
	private def <T extends JobData> getJobs(type : Class<T>) : HashMap<String, T> {
		val filtered_jobs : HashMap<String, T> = new HashMap();
		// extract only jobs of desired type
		for (job : jobs.values()) {
			if (type.isInstance(jobs)) {
				filtered_jobs.put(job.getId(), job as T);
			}
		}
		return filtered_jobs
	}

	/**
	 * The last step a facility has been sensed
	 * 
	 * @param d an object containing the data of a facility
	 * @return last step the facility has been observed/sensed
	 */
	def getStepLastSeen(d : FacilityData) : int {
		return getStepLastSeen(d.getName())
	}

	/** 
	 * The last step a job has been sensed
	 * 
	 * @param d an object containing the data of a job
	 * @return last step the job has been observed/sensed
	 */
	def getStepLastSeen(d : JobData) : int {
		return getStepLastSeen(d.getId())
	}

	def getStepLastSeen(d : EntityData) : int {
		return getStepLastSeen(d.getName())
	}

	/** 
	 * The last step something has been perceived
	 * 
	 * @param id the id of what has been perceived
	 * @return last step the object has been perceived
	 */
	def getStepLastSeen(id : String) : int {
		return this.stepLastSeen.containsKey(id) ? this.stepLastSeen.get(id) : -1
	}


	///////////////////////////////////////////////////////////////////////////////
	//	EVENT CREATION FROM DATA
	// /////////////////////////////////////////////////////////////////////////////
	
	
	/** 
	 * Build a set E_TellPlayerTeamInfo events
	 * 		To send information about team members
	 * 
	 * @return an set of E_TellPlayerTeamInfo events containing the data for each player team-mate
	 */
	@Deprecated
	def getTeamMemberPercepts() : Set<E_TellEntityInfo> {
		var events = new HashSet<E_TellEntityInfo>()
		val players_team = getTeamEntities()

		for (agentName : players_team.keySet) {
			var sources = new HashSet<String>()
			sources.add(agentName)
			events.add(new E_TellPlayerTeamInfo(sources, players_team.get(agentName)))
		}
		return events
	}

	/** 
	 * Build a set E_TellEntityInfoevents
	 * 		To send information about opposition entities
	 * 
	 * @return an set of E_TellEntityInfo events 
	 */
	@Deprecated
	def getGameEntityPercepts() : Set<E_TellEntityInfo> {
		var events = new HashSet<E_TellEntityInfo>()
		val players_opp = getOppositionEntities()

		for (entityName : players_opp.keySet) {
			var ep = new E_TellEntityInfo(players_opp.keySet, players_opp.get(entityName))
			events.add(ep);
		}
		return events

	}

	/** 
	 * Build a set E_ActionStatusInfo
	 * 	To send information about all action result from team members
	 * 
	 * @param entities a map of complete entities with all details filled
	 * @return an set of E_ActionStatusInfo events 
	 */
	@Deprecated
	def getActionPercepts(entities : HashMap<String, EntityData>) : List<E_ActionStatusInfo>{
		var events = new ArrayList<E_ActionStatusInfo>
		
		for (entity : entities.values()) {
			var nameSet = new HashSet<String>()
			nameSet.add(entity.name)
			
			var ap = new E_ActionStatusInfo(nameSet, entity.lastAction)
			events.add(ap)
		}
		return events
		
	}
	
	
	/**
	 * Tools to get FACILITIES
	 * 	shops
	 *  charging station
	 *  workshops
	 *  dumps
	 *  storage
	 *  resource nodes
	 * 
	 * https://github.com/agentcontest/massim/blob/massim-2017-1.7/docs/scenario.md#facilities
	 */



	/** 
	 * Build a set E_ShopInfo
	 * To send information about all shops known
	 * 
	 * @return an set of E_ShopInfo events 
	 */
	@Deprecated
	def getShopSensedPercepts() : List<E_ShopInfo> {
		var events = new ArrayList<E_ShopInfo>

		for (shop : getShops().values) {
			var ssp = new E_ShopInfo(getTeamEntities.keySet, shop);
			events.add(ssp)
		}
		return events
	}

	/** 
	 * Build a set E_ChargingStationInfo
	 * To send information about all charging stations known
	 * 
	 * @return an set of E_ChargingStationInfo events 
	 */
	@Deprecated
	def getChargingStationSensedPercepts() : List<E_ChargingStationInfo>{
		var events = new ArrayList<E_ChargingStationInfo>
		
		for (station : getChargingStations().values){
			var cssp = new E_ChargingStationInfo(getTeamEntities.keySet, station);
			events.add(cssp)
		}
		return events
	}

	/** 
	 * Build a set E_WorkshopInfo
	 * To send information about all workshops known
	 * 
	 * @return an set of E_WorkshopInfo events 
	 */
	@Deprecated
	def getWorkshopSensedPercepts() : List<E_WorkshopInfo> {
		var events = new ArrayList<E_WorkshopInfo>

		for (workshop : getWorkshops().values) {
			var ssp = new E_WorkshopInfo(getTeamEntities.keySet, workshop);
			events.add(ssp)
		}
		return events
	}

	/** 
	 * Build a set E_DumpInfo
	 * To send information about all dumps known
	 * 
	 * @return an set of E_DumpInfo events 
	 */
	@Deprecated
	def getDumpSensedPercepts() : List<E_DumpInfo>{

		var events = new ArrayList<E_DumpInfo>

		for (dump : getDumps().values) {
			var dsp = new E_DumpInfo(getTeamEntities.keySet, dump);
			events.add(dsp)
		}
		return events
	}

	/** 
	 * Build a set E_StorageInfo
	 * To send information about all storage known
	 * 
	 * @return an set of E_StorageInfo events 
	 */
	@Deprecated
	def getStorageSensedPercepts() : List<E_StorageInfo> {
		var events = new ArrayList<E_StorageInfo>

		for (storage : getStorages().values) {
			var ssp = new E_StorageInfo(getTeamEntities.keySet, storage);
			events.add(ssp)
		}
		return events 
	}

	/** 
	 * Build a set E_ResourceInfo
	 * To send information about all resources known
	 * 
	 * @return an set of E_ResourceInfo events 
	 */
	@Deprecated
	def getResourceSensedPercepts() : List<E_ResourceInfo> { 
		var events = new ArrayList<E_ResourceInfo>

		for (resourceNode : getResourceNodes().values) {
			var rsp = new E_ResourceInfo(getTeamEntities.keySet, resourceNode);
			events.add(rsp)
		}
		return events 
	}

	/**
	 * Build a list of E_JobInfo events with regular jobs
	 * 
	 * @returns a list of events each containing a regular job 
	 */
	@Deprecated
	def getRegularJobEvents() : List<E_JobInfo> {
		var events = new ArrayList<E_JobInfo>

		for (job : getRegularJobs().entrySet) {
			var ev = new E_JobInfo(getTeamEntities.keySet, job.value);
			events.add(ev)
		}
		return events
	}

	/** 
	 * Build a list of E_JobInfo events with auction jobs data
	 * 
	 * @returns a list of events each containing a auction job  data
	 */
	@Deprecated
	def getAuctionJobPercepts() : List<E_JobInfo> {
		var events = new ArrayList<E_JobInfo>

		for (job : getAuctionJobs().entrySet) {
			var ev = new E_JobInfo(getTeamEntities.keySet, job.value, "auction");
			events.add(ev)
		}
		return events
	}

	/**
	 * Build a list of E_JobInfo events with mission jobs data
	 *
	 * @returns a list of events each containing a auction job  data
	 */
	@Deprecated
	def getMissionJobPercepts() : List<E_JobInfo> {
		var events = new ArrayList<E_JobInfo>

		for (job : getMissionJobs().entrySet) {
			var ev = new E_JobInfo(getTeamEntities.keySet, job.value, "mission");
			events.add(ev)
		}
		return events
	}
}
